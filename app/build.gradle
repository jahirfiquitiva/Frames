/*
 * Copyright (c) 2017. Jahir Fiquitiva
 *
 * Licensed under the CreativeCommons Attribution-ShareAlike
 * 4.0 International License. You may not use this file except in compliance
 * with the License. You may obtain a copy of the License at
 *
 *    http://creativecommons.org/licenses/by-sa/4.0/legalcode
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion project.ext.compileSdk
    buildToolsVersion project.ext.buildTools

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            output.outputFile = new File(output.outputFile.parent,
                    "MyFramesApp-v${variant.versionName}-${variant.name}.apk")
        }
    }

    lintOptions {
        abortOnError false
        checkReleaseBuilds true
        disable 'MissingTranslation', 'GoogleAppIndexingWarning'
    }

    defaultConfig {
        applicationId "jahirfiquitiva.apps.frames.sample"
        minSdkVersion project.ext.minSdk
        targetSdkVersion project.ext.targetSdk
        versionCode 208
        versionName '2.0.8'
        vectorDrawables.useSupportLibrary = true
    }

    buildTypes {
        release {
            debuggable false
            zipAlignEnabled true
            minifyEnabled false
            shrinkResources false
            proguardFiles 'proguard-android-optimize.txt', 'proguard-rules.pro'
            consumerProguardFiles 'proguard-android-optimize.txt', 'proguard-rules.pro'
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    packagingOptions {
        pickFirst 'META-INF/core_release.kotlin_module'
        pickFirst 'META-INF/library_release.kotlin_module'
    }
}

dependencies {
    // Frames
    compile('me.jahirfiquitiva:Frames:2.0.8@aar') {
        transitive = true
    }
    // Kotlin
    compile 'org.jetbrains.kotlin:kotlin-stdlib:' + project.ext.kotlin
}

// TODO: Remove comment marks if you plan on implementing Firebase Notifications
// apply from: './firebase.gradle'

tasks.whenTaskAdded { task ->
    if (task.name.contains("lint") || task.name.contains("test")) {
        task.enabled = false
    }
}