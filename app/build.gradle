/*
 * Copyright (c) 2018. Jahir Fiquitiva
 *
 * Licensed under the CreativeCommons Attribution-ShareAlike
 * 4.0 International License. You may not use this file except in compliance
 * with the License. You may obtain a copy of the License at
 *
 *    http://creativecommons.org/licenses/by-sa/4.0/legalcode
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
// TODO: Remove comment marks to enable
/*
plugins {
    id 'com.onesignal.androidsdk.onesignal-gradle-plugin' version versions.oneSignalPlugin
}
apply plugin: 'com.onesignal.androidsdk.onesignal-gradle-plugin'
*/

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

// Determines if the application was built on Continuous Integration Server.
def isCi = System.getenv("CI") == "true"

android {
    signingConfigs {
        release {
            keyAlias 'frames'
            keyPassword 'frames'
            storeFile file('frames.jks')
            storePassword 'frames'
        }
    }

    compileSdkVersion versions.targetSdk
    buildToolsVersion versions.buildTools

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            if (isCi) {
                outputFileName = "frames-v${variant.versionName}-${variant.name}-${gitLocalSha()}.apk"
            } else {
                outputFileName = details.appId + "-v${variant.versionName}-${variant.name}.apk"
            }
        }
    }

    lintOptions {
        abortOnError false
        checkReleaseBuilds false
        disable 'MissingTranslation', 'GoogleAppIndexingWarning'
    }

    defaultConfig {
        // TODO: Remove comment marks to enable
        /*
        manifestPlaceholders = [onesignal_app_id               : "abcd-1234-efgh-5678",
                                // Project number pulled from dashboard, local value is ignored.
                                onesignal_google_project_number: "1234567890"]
                                */

        applicationId details.appGroup + "." + details.appId.toLowerCase() + ".sample"
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
        versionCode versions.versionCode
        versionName versions.versionName
        vectorDrawables.useSupportLibrary = true
    }

    buildTypes {
        release {
            debuggable false
            zipAlignEnabled true
            minifyEnabled true
            shrinkResources false
            proguardFiles 'proguard-android-optimize.txt', 'proguard-rules.pro'
            consumerProguardFiles 'proguard-android-optimize.txt', 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    packagingOptions {
        pickFirst 'META-INF/core_debug.kotlin_module'
        pickFirst 'META-INF/core_release.kotlin_module'
        pickFirst 'META-INF/library_debug.kotlin_module'
        pickFirst 'META-INF/library_release.kotlin_module'
    }
}

dependencies {
    implementation project(path: ':library')
    // TODO: Remove comment marks to enable
    // implementation libraries.oneSignal
}

tasks.whenTaskAdded { task ->
    if (task.name.contains("lint") || task.name.contains("test")) {
        task.enabled = false
    }
}

/**
 * Get SHA of the current commit.
 */
def gitLocalSha() {
    def p = 'git rev-parse --short HEAD'.execute([], project.rootDir)
    p.waitFor()
    if (p.exitValue() != 0) {
        return "000"
    }
    return p.text.trim()
}